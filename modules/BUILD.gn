# Copyright 2019 The Triple Banana Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  sources = [
    "public/mojom/authentication.mojom",
    "public/mojom/encrypter.mojom",
    "public/mojom/hello.mojom",
  ]

  public_deps = [
    "//mojo/public/mojom/base",
  ]

  export_class_attribute_blink = "BLINK_PLATFORM_EXPORT"
  export_define_blink = "BLINK_PLATFORM_IMPLEMENTATION=1"
  export_header_blink = "third_party/blink/public/platform/web_common.h"
}

source_set("manifest") {
  sources = [
    "public/manifest/manifest.cc",
    "public/manifest/manifest.h",
  ]

  deps = [
    ":mojom",
    "//base",
  ]
}

source_set("native") {
  sources = [
    "hello/hello.cc",
    "hello/hello.h",
    "public/module_service.cc",
    "public/module_service.h",
  ]

  deps = [
    ":mojom",
    "//services/service_manager/public/cpp",
  ]

  if (is_android) {
    deps += [ ":jni_headers" ]
  }
}

if (is_android) {
  generate_jni("jni_headers") {
    sources = [
      "public/java/org/triple/banana/modules/InterfaceRegistrar.java",
    ]
  }

  android_library("java") {
    java_files = [
      "authentication/java/org/triple/banana/authentication/AuthenticationManagerImpl.java",
      "authentication/java/org/triple/banana/authentication/BiometricPromptAuthenticationActivity.java",
      "authentication/java/org/triple/banana/authentication/FingerprintManagerAuthenticationActivity.java",
      "authentication/java/org/triple/banana/authentication/KeyguardAuthenticationActivity.java",
      "authentication/java/org/triple/banana/authentication/TranslucentActivity.java",
      "encrypter/java/org/triple/banana/encrypter/Encrypter.java",
      "encrypter/java/org/triple/banana/encrypter/EncrypterFactory.java",
      "encrypter/java/org/triple/banana/encrypter/EncrypterManagerImpl.java",
      "encrypter/java/org/triple/banana/encrypter/FakeEncrypter.java",
      "encrypter/java/org/triple/banana/encrypter/KeyStoreEncrypter.java",
      "hello/java/org/triple/banana/hello/HelloImpl.java",
      "public/java/org/triple/banana/modules/InterfaceRegistrar.java",
    ]

    deps = [
      ":mojom_java",
      "//base:base_java",
      "//chrome/android:chrome_java",
      "//mojo/public/java:base_java",
      "//mojo/public/java:bindings_java",
      "//mojo/public/java:system_java",
      "//mojo/public/java/system:system_impl_java",
      "//services/service_manager/public/java:service_manager_java",
      "//services/service_manager/public/mojom:mojom_java",
      "//third_party/android_deps:com_android_support_support_compat_java",
      "//triple_banana:triple_banana_resources",
    ]
  }
}
