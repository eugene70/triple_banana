# Copyright 2019 The Triple Banana Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//mojo/public/tools/bindings/mojom.gni")

source_set("hello") {
  sources = [
    "hello.cc",
    "hello.h",
    "hello_service.cc",
    "hello_service.h",
  ]

  deps = [
    ":mojom",
    "//services/service_manager/public/cpp",
  ]

  if (is_android) {
    deps += [ ":jni_headers" ]
  }
}

mojom("mojom") {
  sources = [
    "public/mojom/constants.mojom",
    "public/mojom/hello.mojom",
  ]

  public_deps = [
    "//mojo/public/mojom/base",
  ]

  export_class_attribute_blink = "BLINK_PLATFORM_EXPORT"
  export_define_blink = "BLINK_PLATFORM_IMPLEMENTATION=1"
  export_header_blink = "third_party/blink/public/platform/web_common.h"
}

source_set("manifest") {
  sources = [
    "public/cpp/manifest.cc",
    "public/cpp/manifest.h",
  ]

  deps = [
    ":mojom",
    "//base",
  ]
}

if (is_android) {
  generate_jni("jni_headers") {
    sources = [
      "android/java/src/org/triple/banana/hello/InterfaceRegistrar.java",
    ]
  }

  android_library("hello_java") {
    java_files = [
      "android/java/src/org/triple/banana/hello/HelloImpl.java",
      "android/java/src/org/triple/banana/hello/InterfaceRegistrar.java",
    ]

    deps = [
      ":mojom_java",
      "//base:base_java",
      "//chrome/android:chrome_java",
      "//mojo/public/java:base_java",
      "//mojo/public/java:bindings_java",
      "//mojo/public/java:system_java",
      "//mojo/public/java/system:system_impl_java",
      "//services/service_manager/public/java:service_manager_java",
      "//services/service_manager/public/mojom:mojom_java",
    ]
  }
}
