# Copyright 2019 The Triple Banana Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

import("//build/config/android/rules.gni")
import("//chrome/android/chrome_public_apk_tmpl.gni")
import("//triple_banana/VERSION")

manifest_package = "org.triple.banana"
triple_banana_manifest = "$target_gen_dir/AndroidManifest.xml"

jinja_template("triple_banana_manifest") {
  # TripleBanana's manifest is based on chrome_public_apk's manifest.
  includes = [ "//chrome/android/java/AndroidManifest.xml" ]
  input = "//triple_banana/src/AndroidManifest.xml"
  output = triple_banana_manifest
  variables = default_chrome_public_jinja_variables + [
                "manifest_package=$manifest_package",
                "min_sdk_version=19",
                "target_sdk_version=$android_sdk_version",
              ]
}

chrome_public_common_apk_or_module_tmpl("triple_banana") {
  target_type = "android_apk"
  apk_name = "TripleBanana"

  android_manifest = triple_banana_manifest
  android_manifest_dep = ":triple_banana_manifest"

  shared_libraries = [ "//chrome/android:libchrome" ]

  # FIXME(#323): We might have to support loading the native library directly
  # from the apk (without extracting) by mmap() or the system linker.
  load_library_from_apk = false

  # The following write/read operations are required to convert number string to
  # integer because there is no conversion utility in GN side.
  _ = "$root_out_dir/conversion.tmp"
  write_file(_, chrome_version_code)
  _chrome_version_code_as_number = read_file(_, "value")
  write_file(_, string_replace(BANANA_VERSION, ".", ""))
  _banana_version_as_number = read_file(_, "value")

  version_code =
      "" + (_banana_version_as_number + _chrome_version_code_as_number)
  version_name = BANANA_VERSION + " @ " + CHROMIUM_VERSION

  deps = [
    ":triple_banana_java",
    "//chrome/android:chrome_apk_pak_assets",
    "//chrome/android:chrome_public_base_module_java",
    "//chrome/android:chrome_public_non_pak_assets",
    "//chrome/android:chrome_public_v8_assets",
    "//third_party/icu:icu_assets",
  ]
}

android_library("triple_banana_java") {
  proguard_configs = [ "//triple_banana/proguard.flags" ]

  sources = [
    # banana_cake layer
    "//triple_banana/banana_cake/bootstrap/java/org/banana/cake/bootstrap/BananaApplication.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeApplicationUtils.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeCommandLine.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeDarkModeUtils.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeFeatureFlags.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeInterfaceProvider.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeTab.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeTabManager.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeToolbarManager.java",

    # src
    "//triple_banana/src/org/triple/banana/CommandLineInitializer.java",
    "//triple_banana/src/org/triple/banana/InterfaceProvider.java",
    "//triple_banana/src/org/triple/banana/TripleBananaApplication.java",

    # adblock
    "//triple_banana/src/org/triple/banana/adblock/FilterLoaderImpl.java",

    # appmenu
    "//triple_banana/src/org/triple/banana/appmenu/AppMenuDelegate.java",

    # authentication
    "//triple_banana/src/org/triple/banana/authentication/ActivityBasedBackend.java",
    "//triple_banana/src/org/triple/banana/authentication/AuthenticationManagerImpl.java",
    "//triple_banana/src/org/triple/banana/authentication/Authenticator.java",
    "//triple_banana/src/org/triple/banana/authentication/Backend.java",
    "//triple_banana/src/org/triple/banana/authentication/BaseActivity.java",
    "//triple_banana/src/org/triple/banana/authentication/BiometricPromptActivity.java",
    "//triple_banana/src/org/triple/banana/authentication/FallbackBackend.java",
    "//triple_banana/src/org/triple/banana/authentication/FingerprintManagerActivity.java",
    "//triple_banana/src/org/triple/banana/authentication/KeyguardActivity.java",
    "//triple_banana/src/org/triple/banana/authentication/SecurityLevelChecker.java",

    # base
    "//triple_banana/src/org/triple/banana/base/InterActivity.java",

    # download
    "//triple_banana/src/org/triple/banana/download/SimpleDownloader.java",

    # encrypter
    "//triple_banana/src/org/triple/banana/encrypter/Encrypter.java",
    "//triple_banana/src/org/triple/banana/encrypter/EncrypterFactory.java",
    "//triple_banana/src/org/triple/banana/encrypter/EncrypterManagerImpl.java",
    "//triple_banana/src/org/triple/banana/encrypter/FakeEncrypter.java",
    "//triple_banana/src/org/triple/banana/encrypter/KeyStoreEncrypter.java",

    # hello
    "//triple_banana/src/org/triple/banana/hello/HelloImpl.java",

    # media
    "//triple_banana/src/org/triple/banana/media/MediaSuspendController.java",

    # password
    "//triple_banana/src/org/triple/banana/password/PasswordExtension.java",

    # remote_config
    "//triple_banana/src/org/triple/banana/remote_config/RemoteConfig.java",

    # secure_dns
    "//triple_banana/src/org/triple/banana/secure_dns/SecureDnsNotificationManager.java",

    # settings
    "//triple_banana/src/org/triple/banana/settings/ExtensionFeatures.java",
    "//triple_banana/src/org/triple/banana/settings/SettingsOpener.java",

    # theme
    "//triple_banana/src/org/triple/banana/theme/DarkModeController.java",

    # toolbar
    "//triple_banana/src/org/triple/banana/toolbar/BottomToolbarButtonDecoration.java",
    "//triple_banana/src/org/triple/banana/toolbar/BottomToolbarController.java",
    "//triple_banana/src/org/triple/banana/toolbar/ButtonId.java",
    "//triple_banana/src/org/triple/banana/toolbar/ButtonTouchHelperCallback.java",
    "//triple_banana/src/org/triple/banana/toolbar/CandidateToolbarButtonAdapter.java",
    "//triple_banana/src/org/triple/banana/toolbar/IToolbarStateChangedObserver.java",
    "//triple_banana/src/org/triple/banana/toolbar/IToolbarStatePersistentStore.java",
    "//triple_banana/src/org/triple/banana/toolbar/OnItemMoveListener.java",
    "//triple_banana/src/org/triple/banana/toolbar/SelectedToolbarButtonAdapter.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarButton.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarButtonItem.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarEditActivity.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarEditController.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarEditor.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarStateModel.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarStatePreferenceStoreImpl.java",
    "//triple_banana/src/org/triple/banana/toolbar/ViewHolderDragListener.java",

    # util
    "//triple_banana/src/org/triple/banana/util/Unzip.java",

    # legacy modules
    "//triple_banana/modules/public/java/org/triple/banana/modules/InterfaceRegistrar.java",
  ]
  deps = [
    ":triple_banana_resources",
    "//base:base_java",
    "//chrome/android:chrome_java",
    "//components/permissions/android:java",
    "//content/public/android:content_java",
    "//mojo/public/java:base_java",
    "//mojo/public/java:bindings_java",
    "//mojo/public/java:system_java",
    "//mojo/public/java/system:system_impl_java",
    "//services/service_manager/public/java:service_manager_java",
    "//services/service_manager/public/mojom:mojom_java",
    "//third_party/android_deps:androidx_appcompat_appcompat_java",
    "//third_party/android_deps:androidx_core_core_java",
    "//third_party/android_deps:androidx_preference_preference_java",
    "//third_party/android_deps:androidx_recyclerview_recyclerview_java",
    "//triple_banana/modules:mojom_java",
    "//ui/android:ui_java",
    "//url:gurl_java",
  ]
}

android_resources("triple_banana_resources") {
  resource_dirs = [
    "//triple_banana/res",
    "//triple_banana/res_override",
  ]

  deps = [ "//chrome/android:chrome_app_java_resources" ]
  custom_package = "org.triple.banana"
}
