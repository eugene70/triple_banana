# Copyright 2019 The Triple Banana Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//chrome/android/chrome_public_apk_tmpl.gni")

manifest_package = "org.triple.banana"
triple_banana_manifest = "$target_gen_dir/AndroidManifest.xml"

jinja_template("triple_banana_manifest") {
  # TripleBanana's manifest is based on chrome_public_apk's manifest.
  includes = [ "//chrome/android/java/AndroidManifest.xml" ]
  input = "//triple_banana/src/AndroidManifest.xml"
  output = triple_banana_manifest
  variables = default_chrome_public_jinja_variables + [
                "manifest_package=$manifest_package",
                "min_sdk_version=19",
                "target_sdk_version=$android_sdk_version",
              ]
}

chrome_public_common_apk_or_module_tmpl("triple_banana") {
  target_type = "android_apk"
  apk_name = "TripleBanana"

  android_manifest = triple_banana_manifest
  android_manifest_dep = ":triple_banana_manifest"

  shared_libraries = [ "//chrome/android:libchrome" ]

  deps = [
    ":triple_banana_assets",
    ":triple_banana_java",
    "//chrome/android:chrome_apk_pak_assets",
    "//chrome/android:chrome_public_base_module_java",
    "//chrome/android:chrome_public_non_pak_assets",
    "//chrome/android:chrome_public_v8_assets",
    "//third_party/icu:icu_assets",
  ]
}

android_library("triple_banana_java") {
  java_files = [
    "//triple_banana/src/org/triple/banana/CommandLineInitializer.java",
    "//triple_banana/src/org/triple/banana/InterfaceProvider.java",
    "//triple_banana/src/org/triple/banana/MediaSuspendController.java",
    "//triple_banana/src/org/triple/banana/TripleBananaApplication.java",
    "//triple_banana/src/org/triple/banana/password/PasswordExtension.java",
    "//triple_banana/src/org/triple/banana/toolbar/BottomToolbarButtonDecoration.java",
    "//triple_banana/src/org/triple/banana/toolbar/BottomToolbarController.java",
    "//triple_banana/src/org/triple/banana/toolbar/ButtonId.java",
    "//triple_banana/src/org/triple/banana/toolbar/ButtonTouchHelperCallback.java",
    "//triple_banana/src/org/triple/banana/toolbar/CandidateToolbarButtonAdapter.java",
    "//triple_banana/src/org/triple/banana/toolbar/IToolbarStateChangedObserver.java",
    "//triple_banana/src/org/triple/banana/toolbar/IToolbarStatePersistentStore.java",
    "//triple_banana/src/org/triple/banana/toolbar/OnItemMoveListener.java",
    "//triple_banana/src/org/triple/banana/toolbar/SelectedToolbarButtonAdapter.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarButton.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarButtonItem.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarEditActivity.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarEditController.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarEditor.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarStateModel.java",
    "//triple_banana/src/org/triple/banana/toolbar/ToolbarStatePreferenceStoreImpl.java",
    "//triple_banana/src/org/triple/banana/toolbar/ViewHolderDragListener.java",
    "//triple_banana/src/org/triple/banana/toolbar/BottomToolbarController.java",
    "//triple_banana/modules/adblock/java/org/triple/banana/adblock/FilterLoaderImpl.java",
    "//triple_banana/modules/authentication/java/org/triple/banana/authentication/AuthenticationManagerImpl.java",
    "//triple_banana/modules/authentication/java/org/triple/banana/authentication/BiometricPromptAuthenticationActivity.java",
    "//triple_banana/modules/authentication/java/org/triple/banana/authentication/FingerprintManagerAuthenticationActivity.java",
    "//triple_banana/modules/authentication/java/org/triple/banana/authentication/KeyguardAuthenticationActivity.java",
    "//triple_banana/modules/authentication/java/org/triple/banana/authentication/TranslucentActivity.java",
    "//triple_banana/modules/encrypter/java/org/triple/banana/encrypter/Encrypter.java",
    "//triple_banana/modules/encrypter/java/org/triple/banana/encrypter/EncrypterFactory.java",
    "//triple_banana/modules/encrypter/java/org/triple/banana/encrypter/EncrypterManagerImpl.java",
    "//triple_banana/modules/encrypter/java/org/triple/banana/encrypter/FakeEncrypter.java",
    "//triple_banana/modules/encrypter/java/org/triple/banana/encrypter/KeyStoreEncrypter.java",
    "//triple_banana/modules/hello/java/org/triple/banana/hello/HelloImpl.java",
    "//triple_banana/modules/public/java/org/triple/banana/modules/InterfaceRegistrar.java",
    "//triple_banana/banana_cake/bootstrap/java/org/banana/cake/bootstrap/BananaApplication.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeCommandLine.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeContextUtils.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeInterfaceProvider.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeTab.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeTabManager.java",
    "//triple_banana/banana_cake/java/org/banana/cake/CakeToolbarManager.java",
  ]
  deps = [
    ":triple_banana_resources",
    "//base:base_java",
    "//chrome/android:chrome_java",
    "//content/public/android:content_java",
    "//mojo/public/java:base_java",
    "//mojo/public/java:bindings_java",
    "//mojo/public/java:system_java",
    "//mojo/public/java/system:system_impl_java",
    "//services/service_manager/public/java:service_manager_java",
    "//services/service_manager/public/mojom:mojom_java",
    "//third_party/android_deps:com_android_support_appcompat_v7_java",
    "//third_party/android_deps:com_android_support_preference_v7_java",
    "//third_party/android_deps:com_android_support_recyclerview_v7_java",
    "//third_party/android_deps:com_android_support_support_compat_java",
    "//triple_banana/modules:mojom_java",
    "//ui/android:ui_full_java",
  ]
}

android_resources("triple_banana_resources") {
  resource_dirs = [ "//triple_banana/res" ]

  deps = [
    "//chrome/android:chrome_public_apk_resources",
  ]
  custom_package = "org.triple.banana"
}

android_assets("triple_banana_assets") {
  sources = [
    "//triple_banana/modules/adblock/filter/Filters",
  ]
}
